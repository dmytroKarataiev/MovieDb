/*
 *  The MIT License (MIT)
 *
 *  Copyright (c) 2016. Dmytro Karataiev
 *
 *  Permission is hereby granted, free of charge, to any person obtaining a copy
 *  of this software and associated documentation files (the "Software"), to deal
 *  in the Software without restriction, including without limitation the rights
 *  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 *  copies of the Software, and to permit persons to whom the Software is
 *  furnished to do so, subject to the following conditions:
 *
 *  The above copyright notice and this permission notice shall be included in all
 *  copies or substantial portions of the Software.
 *
 *  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 *  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 *  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 *  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 *  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 *  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 *  SOFTWARE.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId "com.adkdevelopment.moviesdb"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 2
        versionName "0.1"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        lintConfig rootProject.file('gradle/lint.xml')
    }
}

// If we have a property and a config file - apply configs and flavors from there
def propertyConfig = 'moviesdb.signing'
def signingFile = 'moviesdb.gradle'

if (project.hasProperty(propertyConfig)
        && file(project.property(propertyConfig) + signingFile).exists()) {
    apply from: project.property(propertyConfig) + signingFile;
} else {
    android {
        buildTypes.each {
            it.buildConfigField 'String', 'MOVIE_DB_API_KEY', "\"\""
            it.buildConfigField 'String', 'YOUTUBE_API_KEY', "\"\""
        }
    }
}

ext {
    compatLibVersion = '25.4.0'
    butterknife = '8.6.0'
    retrofit = '2.2.0'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':license-release')
    testCompile 'junit:junit:4.12'
    compile "com.android.support:appcompat-v7:$compatLibVersion"
    compile "com.android.support:design:$compatLibVersion"
    compile "com.android.support:palette-v7:$compatLibVersion"
    compile "com.android.support:cardview-v7:$compatLibVersion"

    // injections
    compile "com.jakewharton:butterknife:$butterknife"
    kapt "com.jakewharton:butterknife-compiler:$butterknife"
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'
    compile 'org.jetbrains:annotations:13.0'

    // image downloader
    compile 'com.squareup.picasso:picasso:2.5.2'

    // download and parse data
    compile 'com.google.code.gson:gson:2.8.0'

    // data fetching and converting to objects
    compile "com.squareup.retrofit2:retrofit:$retrofit"
    compile "com.squareup.retrofit2:converter-gson:$retrofit"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofit"

    // RxJava + RxAndroid
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.2.7'

    // fix memory leaks if they are present
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true
    }
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}
repositories {
    mavenCentral()
}